
#+vet !unused-imports

package gpu

import "base:runtime"
import intr "base:intrinsics"
import "core:slice"
import "core:sync"
import mem "core:mem"
import "core:math"

import sdl "vendor:sdl3"
import vk "vendor:vulkan"

// Pointer
ptr_advance :: proc(addr: ptr, #any_int offset: i64) -> ptr
{
    res := addr
    if res.cpu != nil { res.cpu = rawptr(uintptr(res.cpu) + uintptr(offset)) }
    res.gpu.ptr = rawptr(uintptr(res.gpu.ptr) + uintptr(offset))
    return res
}

// Slice
// end == -1 means "until the end"
subslice :: #force_inline proc(s: slice_t($T), #any_int start: i64, #any_int end: i64 = -1) -> slice_t(T)
{
    end_clean := end if end != -1 else i64(len(s.cpu))
    assert(start >= 0)
    assert(start < i64(len(s.cpu)))
    assert(end_clean >= 0)
    assert(end_clean <= i64(len(s.cpu)))
    res := s
    res.cpu = res.cpu[start:end_clean]
    res.gpu.ptr = rawptr(uintptr(res.gpu.ptr) + uintptr(start * size_of(T)))
    return res
}

slice_len :: #force_inline proc(s: slice_t($T)) -> i64
{
    return i64(len(s.cpu))
}

slice_to_ptr :: #force_inline proc(s: slice_t($T)) -> ptr_t(T)
{
    return {
        cpu = raw_data(s.cpu),
        gpu = s.gpu,
    }
}

// Type-safe variants of raw procedures
cmd_draw_indexed :: #force_inline proc(cmd_buf: Command_Buffer, vertex_data, fragment_data: gpuptr, indices: slice_t($T),
                                       instance_count: u32 = 1, loc := #caller_location)
{
    #assert(T == u32 || T == u16)
    idx_fmt: Index_Format = .U32 when T == u32 else .U16
    cmd_draw_indexed_raw(cmd_buf, vertex_data, fragment_data, indices, idx_fmt, u32(slice_len(indices)), instance_count, loc)
}

cmd_dispatch_indirect :: #force_inline proc(cmd_buf: Command_Buffer, compute_data: gpuptr,
                                            arguments: ptr_t(Dispatch_Indirect_Command), loc := #caller_location)
{
    cmd_dispatch_indirect_raw(cmd_buf, compute_data, arguments, loc)
}

cmd_draw_indexed_indirect :: #force_inline proc(cmd_buf: Command_Buffer, vertex_data, fragment_data: gpuptr, indices: slice_t($T),
                                                indirect_arguments: ptr_t($T2), loc := #caller_location)
{
    #assert(T == u32 || T == u16)
    idx_fmt: Index_Format = .U32 when T == u32 else .U16
    cmd_draw_indexed_indirect_raw(cmd_buf, vertex_data, fragment_data, indices, idx_fmt, indirect_arguments, loc)
}

cmd_draw_indexed_indirect_multi :: #force_inline proc(cmd_buf: Command_Buffer, vertex_data, fragment_data: gpuptr, indices: slice_t($T),
                                                      indirect_arguments: slice_t($T2), draw_count: ptr_t(u32), loc := #caller_location)
{
    #assert(T == u32 || T == u16)
    idx_fmt: Index_Format = .U32 when T == u32 else .U16
    cmd_draw_indexed_indirect_multi_raw(cmd_buf, vertex_data, fragment_data, indices, idx_fmt, indirect_arguments, size_of(T2), draw_count, loc)
}

cmd_draw_mesh_tasks_indirect :: #force_inline proc(cmd_buf: Command_Buffer, mesh_data, fragment_data: gpuptr, indirect_arguments: ptr_t($T), loc := #caller_location)
{
    #assert(type_of(indirect_arguments.cpu.cmd) == Draw_Mesh_Tasks_Indirect_Command)
    _cmd_draw_mesh_tasks_indirect_raw(cmd_buf, mesh_data, fragment_data, indirect_arguments, loc)
}

cmd_draw_mesh_tasks_indirect_multi :: #force_inline proc(cmd_buf: Command_Buffer, mesh_data, fragment_data: gpuptr,
                                                         indirect_arguments: slice_t($T), draw_count: ptr_t(u32), loc := #caller_location)
{
    #assert(type_of(indirect_arguments.cpu[0].cmd) == Draw_Mesh_Tasks_Indirect_Command)
    _cmd_draw_mesh_tasks_indirect_multi_raw(cmd_buf, mesh_data, fragment_data, indirect_arguments, size_of(T), draw_count, loc)
}

// Memory

ptr_t :: struct($T: typeid)
{
    cpu: ^T,
    using gpu: gpuptr,
}

slice_t :: struct($T: typeid)
{
    cpu: []T,
    using gpu: gpuptr,
}

mem_alloc_ptr :: #force_inline proc($T: typeid, mem_type := Memory.Default, loc := #caller_location) -> ptr_t(T)
{
    p := mem_alloc_raw(size_of(T), 1, align_of(T), mem_type = mem_type, loc = loc)
    return ptr_t(T) {
        cpu = cast(^T) p.cpu,
        gpu = p.gpu
    }
}

mem_alloc_slice :: #force_inline proc($T: typeid, #any_int count: i32, mem_type := Memory.Default, loc := #caller_location) -> slice_t(T)
{
    p := mem_alloc_raw(size_of(T), count, align_of(T), mem_type = mem_type, loc = loc)
    return slice_t(T) {
        cpu = slice.from_ptr(cast(^T)p.cpu, int(count)),
        gpu = p.gpu
    }
}

mem_alloc :: proc {
    mem_alloc_ptr,
    mem_alloc_slice,
}

mem_free_ptr :: #force_inline proc(addr: ptr_t($T), loc := #caller_location)
{
    mem_free_raw(addr.gpu, loc = loc)
}

mem_free_slice :: #force_inline proc(addr: slice_t($T), loc := #caller_location)
{
    mem_free_raw(addr.gpu, loc = loc)
}

mem_free :: proc {
    mem_free_ptr,
    mem_free_slice,
}

cmd_mem_copy_ptr :: #force_inline proc(cmd_buf: Command_Buffer, dst: ptr_t($T), src: ptr_t(T), loc := #caller_location)
{
    cmd_mem_copy_raw(cmd_buf, dst.gpu, src.gpu, size_of(T), loc = loc)
}

cmd_mem_copy_slice :: proc(cmd_buf: Command_Buffer, dst: slice_t($T), src: slice_t(T), loc := #caller_location)
{
    cmd_mem_copy_raw(cmd_buf, dst.gpu, src.gpu, size_of(T) * min(slice_len(dst), slice_len(src)), loc = loc)
}

cmd_mem_copy :: proc {
    cmd_mem_copy_ptr,
    cmd_mem_copy_slice,
}

// Simple linear allocator. Not thread-safe, as it is meant for
// temporary, thread-local allocations (e.g. staging buffers).
Arena :: struct
{
    block_size: i64,
    mem_type: Memory,

    offset: i64,
    block_idx: i64,
    blocks: [dynamic]Arena_Block,
}

@(private="file")
Arena_Block :: struct
{
    p: ptr,
    size: i64,
}

arena_create :: proc(#any_int block_size: i64 = 4*1024*1024, mem_type := Memory.Default, loc := #caller_location) -> Arena
{
    assert(block_size > 0, "block_size must be positive")

    res: Arena
    res.block_size = block_size
    res.mem_type = mem_type
    first_block := Arena_Block {
        p = mem_alloc_raw(block_size, 1, 16, mem_type = mem_type, loc = loc),
        size = block_size,
    }
    append(&res.blocks, first_block)
    return res
}

arena_alloc_raw :: proc(arena: ^Arena, #any_int el_size: i64, #any_int el_count: i64, #any_int align: i32 = 16) -> ptr
{
    assert(arena.block_size > 0, "Arena is not initialized! Did you call arena_create()?")

    bytes := el_size * el_count
    assert(bytes >= 0 && align > 0)

    if bytes == 0 do return {}

    block := arena.blocks[arena.block_idx]

    // If we request an alignment of > 16 and cpu/gpu are only aligned to 16,
    // it's impossible to find the same offset for both.
    if block.p.cpu != nil && uintptr(block.p.cpu) % uintptr(align) != uintptr(block.p.gpu.ptr) % uintptr(align) {
        panic("Could not satisfy alignment requirements in GPU arena allocation.")
    }

    gpu_addr := uintptr(block.p.gpu.ptr) + uintptr(arena.offset)
    arena.offset = i64(align_up(u64(gpu_addr), u64(align)) - u64(uintptr(block.p.gpu.ptr)))
    if arena.offset + bytes > block.size {
        block = arena_next_block(arena, bytes, align)
        arena.offset = 0
    }

    suballoc_ptr := mem_suballoc(block.p, arena.offset, el_size, el_count)
    arena.offset += bytes

    return suballoc_ptr

    arena_next_block :: proc(arena: ^Arena, bytes: i64, align: i32) -> Arena_Block
    {
        arena.block_idx += 1
        arena.offset = 0
        if arena.block_idx >= i64(len(arena.blocks))
        {
            new_size := max(arena.block_size, bytes)
            new_p := mem_alloc_raw(new_size, 1, align, mem_type = arena.mem_type)
            new_block := Arena_Block { p = new_p, size = new_size }
            append(&arena.blocks, new_block)
            return new_block
        }
        else
        {
            if arena.blocks[arena.block_idx].size >= bytes
            {
                return arena.blocks[arena.block_idx]
            }
            else
            {
                mem_free_raw(arena.blocks[arena.block_idx].p.gpu)
                new_size := max(arena.block_size, bytes)
                new_p := mem_alloc_raw(new_size, 1, align, mem_type = arena.mem_type)
                new_block := Arena_Block { p = new_p, size = new_size }
                arena.blocks[arena.block_idx] = new_block
                return new_block
            }
        }
    }
}

arena_alloc_ptr :: #force_inline proc(arena: ^Arena, $T: typeid) -> ptr_t(T)
{
    return transmute(ptr_t(T)) arena_alloc_raw(arena, size_of(T), 1, align_of(T))
}

arena_alloc_slice :: #force_inline proc(arena: ^Arena, $T: typeid, #any_int count: i32) -> slice_t(T)
{
    p_raw := arena_alloc_raw(arena, size_of(T), count, align_of(T))
    return slice_t(T) {
        cpu = slice.from_ptr(cast(^T) p_raw.cpu, int(count)),
        gpu = p_raw.gpu
    }
}

arena_alloc :: proc {
    arena_alloc_ptr,
    arena_alloc_slice,
}

arena_free_all :: proc(arena: ^Arena)
{
    arena.offset = 0
    arena.block_idx = 0
}

arena_destroy :: proc(arena: ^Arena, loc := #caller_location)
{
    for block in arena.blocks {
        mem_free_raw(block.p.gpu, loc = loc)
    }
    delete(arena.blocks)
    arena^ = {}
}

Owned_Texture :: struct
{
    using tex: Texture,
    mem: gpuptr,
}

texture_alloc_and_create :: proc(desc: Texture_Desc, queue: Queue = nil, signal_sem: Semaphore = {}, signal_value: u64 = 0, name := "", loc := #caller_location) -> Owned_Texture
{
    size, align := texture_size_and_align(desc, loc = loc)
    ptr := mem_alloc_raw(size, 1, align, .GPU, loc = loc)
    texture := texture_create(desc, ptr, queue, signal_sem, signal_value, name = name, loc = loc)
    return Owned_Texture { texture, ptr.gpu }
}

texture_free_and_destroy :: proc(texture: ^Owned_Texture, loc := #caller_location)
{
    texture_destroy(texture, loc = loc)
    mem_free_raw(texture.mem, loc = loc)
    texture^ = {}
}

Owned_BVH :: struct
{
    using handle: BVH,
    mem: gpuptr,
}

blas_alloc_and_create :: proc(desc: BLAS_Desc, loc := #caller_location) -> Owned_BVH
{
    size, align := bvh_size_and_align(desc, loc = loc)
    ptr := mem_alloc_raw(size, 1, align, .GPU, loc = loc)
    bvh := bvh_create(desc, ptr, loc = loc)
    return Owned_BVH { bvh, ptr }
}

tlas_alloc_and_create :: proc(desc: TLAS_Desc, loc := #caller_location) -> Owned_BVH
{
    size, align := bvh_size_and_align(desc, loc = loc)
    ptr := mem_alloc_raw(size, 1, align, .GPU, loc = loc)
    bvh := bvh_create(desc, ptr, loc = loc)
    return Owned_BVH { bvh, ptr }
}

bvh_alloc_and_create :: proc { blas_alloc_and_create, tlas_alloc_and_create }

bvh_free_and_destroy :: proc(bvh: ^Owned_BVH, loc := #caller_location)
{
    bvh_destroy(bvh, loc = loc)
    mem_free_raw(bvh.mem, loc = loc)
    bvh^ = {}
}

blas_alloc_build_scratch_buffer :: proc(arena: ^Arena, desc: BLAS_Desc, loc := #caller_location) -> ptr
{
    size, align := blas_build_scratch_buffer_size_and_align(desc, loc = loc)
    return arena_alloc_raw(arena, size, 1, align)
}

tlas_alloc_build_scratch_buffer :: proc(arena: ^Arena, desc: TLAS_Desc, loc := #caller_location) -> ptr
{
    size, align := tlas_build_scratch_buffer_size_and_align(desc, loc = loc)
    return arena_alloc_raw(arena, size, 1, align)
}

bvh_alloc_build_scratch_buffer :: proc { blas_alloc_build_scratch_buffer, tlas_alloc_build_scratch_buffer }

// Swapchain utils

swapchain_create_from_sdl :: proc(window: ^sdl.Window, frames_in_flight: u32, present_mode: Present_Mode = {})
{
    vk_surface: vk.SurfaceKHR
    ok := sdl.Vulkan_CreateSurface(window, vk_get_instance(), nil, &vk_surface)
    ensure(ok, "Could not create surface.")

    window_size_x: i32
    window_size_y: i32
    sdl.GetWindowSize(window, &window_size_x, &window_size_y)
    swapchain_create(vk_surface, { u32(max(0, window_size_x)), u32(max(0, window_size_y)) }, frames_in_flight, present_mode)
}

// Texture utils

cmd_generate_mipmaps :: proc(cmd_buf: Command_Buffer, texture: Texture)
{
    for mip in 1..<texture.mip_count
    {
        if mip > 1 {
            cmd_barrier(cmd_buf, .Transfer, .Transfer)
        }

        src := Blit_Rect { mip_level = mip - 1 }
        dst := Blit_Rect { mip_level = mip }
        cmd_blit_texture(cmd_buf, texture, dst, texture, src, .Linear)
    }
}

// Scoped procs

@(private="file")
Scoped_Render_Pass_Out :: struct
{
    cmd_buf: Command_Buffer,
    loc: runtime.Source_Code_Location,
}

@(deferred_out = cmd_scoped_render_pass_end)
cmd_scoped_render_pass :: #force_inline proc(cmd_buf: Command_Buffer, desc: Render_Pass_Desc, loc := #caller_location) -> Scoped_Render_Pass_Out
{
    cmd_begin_render_pass(cmd_buf, desc, loc)
    return { cmd_buf, loc }
}

@(private="file")
cmd_scoped_render_pass_end :: #force_inline proc(scope_out: Scoped_Render_Pass_Out)
{
    cmd_end_render_pass(scope_out.cmd_buf, scope_out.loc)
}

@(private="file")
Scoped_Debug_Label_Out :: struct
{
    cmd_buf: Command_Buffer,
    loc: runtime.Source_Code_Location,
}

@(deferred_out = cmd_scoped_debug_label_end)
cmd_scoped_debug_label :: #force_inline proc(cmd_buf: Command_Buffer, name: string, color: [4]f32, loc := #caller_location) -> Scoped_Debug_Label_Out
{
    cmd_begin_debug_label(cmd_buf, name, color, loc)
    return { cmd_buf, loc }
}

@(private="file")
cmd_scoped_debug_label_end :: #force_inline proc(scope_out: Scoped_Debug_Label_Out)
{
    cmd_end_debug_label(scope_out.cmd_buf, scope_out.loc)
}

// Descriptors

@(private="file")
Descriptor_Pool_Freelist :: struct
{
    el_count: u8,
    free: [dynamic]u32,
}

@(private="file")
Descriptor_Pool_Resource :: struct($T: typeid)
{
    res_count: u32,  // Current number of allocated descriptors in addr.
    res_capacity: u32,
    lock: sync.Atomic_Mutex,
    freelists: [dynamic]Descriptor_Pool_Freelist,  // One freelist per allocation size.
    alloc_size: [dynamic]u8,  // byte i contains the number of descriptors for allocation of index i.
    default_res: T,
}

// Simple allocator of descriptor indices. Thread-safe.
Descriptor_Pool :: struct
{
    using heap: Descriptor_Heap,
    texture_pool: Descriptor_Pool_Resource(Texture_Descriptor),
    texture_rw_pool: Descriptor_Pool_Resource(Texture_Descriptor),
    sampler_pool: Descriptor_Pool_Resource(Sampler_Descriptor),
    bvh_pool: Descriptor_Pool_Resource(BVH),
}

// NOTE: There can be fragmentation so make sure you reserve more
// slots than are actually needed. Most of the time a single global
// descriptor pool is good enough and for that, these defaults are
// fairly reasonable.
desc_pool_create :: proc(texture_count: u32 = 65536,
                         texture_rw_count: u32 = 65536,
                         sampler_count: u32 = 32,
                         bvh_count: u32 = 16,
                         loc := #caller_location) -> Descriptor_Pool
{
    res: Descriptor_Pool
    res.texture_pool = desc_pool_resource_init(i64(texture_count), Texture_Descriptor)
    res.sampler_pool = desc_pool_resource_init(i64(sampler_count), Sampler_Descriptor)
    res.texture_rw_pool = desc_pool_resource_init(i64(texture_rw_count), Texture_Descriptor)
    res.bvh_pool = desc_pool_resource_init(i64(bvh_count), BVH)
    res.heap = desc_heap_create(texture_count, texture_rw_count, sampler_count, bvh_count, loc = loc)
    return res
}

desc_pool_destroy :: proc(pool: ^Descriptor_Pool, loc := #caller_location)
{
    desc_pool_resource_destroy(&pool.texture_pool)
    desc_pool_resource_destroy(&pool.texture_rw_pool)
    desc_pool_resource_destroy(&pool.sampler_pool)
    desc_pool_resource_destroy(&pool.bvh_pool)
    desc_heap_destroy(pool.heap, loc = loc)
    pool^ = {}
}

desc_pool_free_textures :: #force_inline proc(pool: ^Descriptor_Pool, idx: u32) {
    desc_pool_resource_free(&pool.texture_pool, idx)
}
desc_pool_free_textures_rw :: #force_inline proc(pool: ^Descriptor_Pool, idx: u32) {
    desc_pool_resource_free(&pool.texture_rw_pool, idx)
}
desc_pool_free_samplers :: #force_inline proc(pool: ^Descriptor_Pool, idx: u32) {
    desc_pool_resource_free(&pool.sampler_pool, idx)
}
desc_pool_free_bvhs :: #force_inline proc(pool: ^Descriptor_Pool, idx: u32) {
    desc_pool_resource_free(&pool.bvh_pool, idx)
}

desc_pool_free_all :: proc(pool: ^Descriptor_Pool)
{
    desc_pool_resource_free_all(&pool.texture_pool)
    desc_pool_resource_free_all(&pool.texture_rw_pool)
    desc_pool_resource_free_all(&pool.sampler_pool)
    desc_pool_resource_free_all(&pool.bvh_pool)
}

desc_pool_alloc_texture :: proc(pool: ^Descriptor_Pool, texture: Texture_Descriptor) -> u32 {
    return desc_pool_alloc_textures(pool, { texture })
}
desc_pool_alloc_texture_rw :: proc(pool: ^Descriptor_Pool, texture: Texture_Descriptor) -> u32 {
    return desc_pool_alloc_textures_rw(pool, { texture })
}
desc_pool_alloc_sampler :: proc(pool: ^Descriptor_Pool, sampler: Sampler_Descriptor) -> u32 {
    return desc_pool_alloc_samplers(pool, { sampler })
}
desc_pool_alloc_bvh :: proc(pool: ^Descriptor_Pool, bvh: BVH) -> u32 {
    return desc_pool_alloc_bvhs(pool, { bvh })
}

// Passing multiple descriptors to desc_pool_alloc_X is useful for contiguous descriptors.
// One usecase for this is to group descriptors into contiguous sets. This enables grouping
// based on update frequency and so on. In the shader you can store a single index and then
// do something like:
// texture_sample(material_base_id + 0, ...);
// texture_sample(material_base_id + 1, ...);
// texture_sample(material_base_id + 2, ...);
desc_pool_alloc_textures :: proc(pool: ^Descriptor_Pool, textures: []Texture_Descriptor) -> u32
{
    assert(len(textures) <= int(max(u8)))
    idx := desc_pool_resource_alloc(&pool.texture_pool, i64(len(textures)))
    desc_heap_set_textures(pool.heap, idx, textures)
    return idx
}

desc_pool_alloc_textures_rw :: proc(pool: ^Descriptor_Pool, textures_rw: []Texture_Descriptor) -> u32
{
    assert(len(textures_rw) <= int(max(u8)))
    idx := desc_pool_resource_alloc(&pool.texture_rw_pool, i64(len(textures_rw)))
    desc_heap_set_textures_rw(pool.heap, idx, textures_rw)
    return idx
}

desc_pool_alloc_samplers :: proc(pool: ^Descriptor_Pool, samplers: []Sampler_Descriptor) -> u32
{
    assert(len(samplers) <= int(max(u8)))
    idx := desc_pool_resource_alloc(&pool.sampler_pool, i64(len(samplers)))
    desc_heap_set_samplers(pool.heap, idx, samplers)
    return idx
}

desc_pool_alloc_bvhs :: proc(pool: ^Descriptor_Pool, bvhs: []BVH) -> u32
{
    assert(len(bvhs) <= int(max(u8)))
    idx := desc_pool_resource_alloc(&pool.bvh_pool, i64(len(bvhs)))
    desc_heap_set_bvhs(pool.heap, idx, bvhs)
    return idx
}

@(private="file")
desc_pool_resource_init :: proc(res_count: i64, $T: typeid) -> Descriptor_Pool_Resource(T)
{
    res: Descriptor_Pool_Resource(T)
    res.res_count = 0
    res.res_capacity = u32(res_count)
    return res
}

@(private="file")
desc_pool_resource_alloc :: proc(pool: ^Descriptor_Pool_Resource($T), count: i64) -> u32
{
    assert(count > 0)
    assert(count <= i64(max(u8)))
    assert(count <= 16, "Descriptor_Pool is built for small allocation sizes.")
    sync.guard(&pool.lock)

    found: ^Descriptor_Pool_Freelist
    for &freelist in pool.freelists
    {
        if len(freelist.free) <= 0 do continue

        if i64(freelist.el_count) == count {
            found = &freelist
            break
        }
    }

    if found != nil
    {
        free_slot := pop(&found.free)
        pool.alloc_size[free_slot] = u8(count)
        return free_slot
    }
    else
    {
        assert(pool.res_count + u32(count) < pool.res_capacity)
        free_slot := pool.res_count
        pool.res_count += u32(count)
        resize(&pool.alloc_size, pool.res_count)
        pool.alloc_size[free_slot] = u8(count)
        return free_slot
    }
}

@(private="file")
desc_pool_resource_free :: proc(pool: ^Descriptor_Pool_Resource($T), idx: u32)
{
    sync.guard(&pool.lock)

    count := pool.alloc_size[idx]

    found: ^Descriptor_Pool_Freelist
    for &freelist in pool.freelists
    {
        if len(freelist.free) <= 0 do continue

        if freelist.el_count == count {
            found = &freelist
            break
        }
    }

    if found == nil
    {
        append(&pool.freelists, Descriptor_Pool_Freelist {
            el_count = count,
            free = {},
        })

        found = &pool.freelists[len(pool.freelists)-1]
    }

    append(&found.free, idx)
}

@(private="file")
desc_pool_resource_free_all :: proc(pool: ^Descriptor_Pool_Resource($T))
{
    for &freelist in pool.freelists do delete(freelist.free)
    delete(pool.freelists)
    pool.res_count = 0
}

@(private="file")
desc_pool_resource_destroy :: proc(pool: ^Descriptor_Pool_Resource($T))
{
    desc_pool_resource_free_all(pool)
}
